ClearAll[hx, hy, hz, h0]


(*Bloch Sphere components*)
hx[kx_, ky_] := 
  t1*(1 + Cos[-Sqrt[3]/2*kx + 3/2*ky] + Cos[-Sqrt[3]/2*kx - 3/2*ky]) +
    t3*(2*Cos[-Sqrt[3]*kx] + Cos[3*ky]);
hy[kx_, ky_] := 
  t1*(Sin[-Sqrt[3]/2*kx + 3/2*ky] - Sin[-Sqrt[3]/2*kx - 3/2*ky]) + 
   t3*Sin[3*ky];
hz[kx_, ky_] := 
  M - 2*t2*Sin[\[Phi]]*(Sin[-Sqrt[3]/2*kx + 3/2*ky] + 
      Sin[-Sqrt[3]/2*kx - 3/2*ky] + Sin[Sqrt[3]*kx]);
h0[kx_, ky_] := 
  2*t2*Cos[\[Phi]]*(Cos[-Sqrt[3]/2*kx + 3/2*ky] + 
     Cos[-Sqrt[3]/2*kx - 3/2*ky] + Cos[Sqrt[3]*kx]);
     
(*Geometric winding*)
Manipulate[
 Show@{ParametricPlot3D[{hx[kx, ky], hy[kx, ky], 
      hz[kx, ky] + h0[kx, ky]} /. {M -> m, \[Phi] -> phi, t1 -> 1, 
      t2 -> 1/3, t3 -> tt}, {kx, -\[Pi], \[Pi]}, {ky, -\[Pi], \[Pi]},
    PlotPoints -> ControlActive[10, 20],(*动态交互优化命令,拖动滑块时PlotPoints=10,显示时为20*)
    ColorFunction -> "Aquamarine",
    MeshFunctions -> {#1 &, #2 &, #3 &},(*代表按照x=f(x),y=g(y),z=r(z)规则进行mesh*)
    Mesh -> {{0}, {0}, {0}},(*在x=0,y=0,z=0处画线条*)
    MeshStyle -> {Directive[Thick, Blue], Directive[Thick, Red], 
      Directive[Thick, Orange]},
    PlotStyle -> Directive[Opacity[0.2]],
    BoxRatios -> {1, 1, 1},
    PlotRange -> All
    ],
   Graphics3D[{PointSize[0.04], Red, Point[{0, 0, 0}]}]},
 {{m, 0, "M"}, -Sqrt[3], Sqrt[3]},
 {{phi, \[Pi]/2, "\[CapitalPhi]"}, -\[Pi], \[Pi]},
 {{tt, 0, ToString[t3, TraditionalForm]}, 0, 2}
 ]
